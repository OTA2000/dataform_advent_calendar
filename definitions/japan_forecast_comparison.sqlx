config {
  type: "table",
  schema: "covid19",
  name: "japan_forecast_comparison",
  description: "予実比較(日別)",
  columns: {
    date: "日",
    prefecture_name_kanji: "都道府県名",
    forecast_new_confirmed: "新規感染者数(予測値)",
    new_confirmed: "新規感染者数",
    forecast_new_deaths: "新規死者数(予測値)",
    new_deaths: "新規死者数"
  }
}

# standardSQL
WITH t AS (
  SELECT
    f.prefecture_name_kanji,
    f.prediction_date AS date,
    f.forecast_date,
    MAX (f.forecast_date) OVER (
      PARTITION BY f.prefecture_code,
      f.prediction_date
    ) AS latest_forecast_date,
    f.new_confirmed AS forecast_new_confirmed,
    j.new_confirmed,
    f.new_deaths AS forecast_new_deaths,
    j.new_deaths,
  FROM
    ${ref("japan_forecast")} f
    LEFT JOIN ${ref("japan_actual")} j ON f.prediction_date = j.date
    AND f.prefecture_code = j.prefecture_code
  WHERE
    f.prediction_date >= "2020-12-21"
)
SELECT
  date,
  prefecture_name_kanji,
  forecast_new_confirmed,
  new_confirmed,
  forecast_new_deaths,
  new_deaths
FROM
  t
